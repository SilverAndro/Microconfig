plugins {
    id 'fabric-loom' version '1.7.+'
    id 'maven-publish'
}

sourceCompatibility = JavaVersion.VERSION_17
targetCompatibility = JavaVersion.VERSION_17

archivesBaseName = project.archives_base_name
version = project.mod_version
group = project.maven_group

dependencies {
    //to change the versions see the gradle.properties file
    minecraft "com.mojang:minecraft:${project.minecraft_version}"
    mappings "net.fabricmc:yarn:${project.yarn_mappings}:v2"
    modImplementation "net.fabricmc:fabric-loader:${project.loader_version}"
}

processResources {
    inputs.property "version", project.version

    filesMatching("fabric.mod.json") {
        expand "version": project.version
    }
}

java {
    withSourcesJar()
}

jar {
    from "LICENSE"
}

publishing {
    publications {
        create("microconfig", MavenPublication) {
            from(components["java"])
            artifactId 'microconfig'

            pom {
                name.set("Microconfig")
                description.set("Tiny custom config system")
                inceptionYear.set("2020")
                licenses {
                    license {
                        name.set("MPL-2.0")
                        distribution.set("repo")
                    }
                }
                scm {
                    url.set("https://github.com/SilverAndro/microconfig")
                }
                issueManagement {
                    system.set("Github")
                    url.set("https://github.com/SilverAndro/microconfig/issues")
                }
                developers {
                    developer {
                        name.set("Silver")
                        email.set("me@silverando.dev")
                        url.set("silverandro.dev")
                    }
                }
            }
        }
    }

    repositories {
        maven {
            name = "SilverMaven"
            url = uri("https://maven.silverandro.dev")
            credentials {
                username = System.getProperty("silverMavenUsername")
                password = System.getProperty("silverMavenPassword")
            }
        }
    }
}
